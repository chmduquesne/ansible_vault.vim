Execute(Plugin loads):
  Assert exists('g:loaded_ansible_vault_auto')


Execute(Vault file is decrypted on read):

  execute 'edit test/fixtures/vault.yml'
  " SafeState never triggers with vader, we have to trigger it manually
  doautocmd SafeState

  let line = getline(1)

  let plain_yml = readfile('test/fixtures/plain.yml')
  let plain_line = plain_yml[0]

  " Buffer should now contain decrypted plain_yml
  Assert line ==# plain_line

  bwipeout!


Execute (New vault file is encrypted on write):

  execute 'edit test/fixtures/new_vault.yml'

  call setline(1, 'secret_key: supersecret')

  write

  bwipeout!

  " Re-open raw file without plugin interfering
  let content = readfile('test/fixtures/new_vault.yml')

  Assert content[0] =~ '^\$ANSIBLE_VAULT'

  call delete('test/fixtures/new_vault.yml')


Execute (Modified vault file is re-encrypted on write):

  call system('cp test/fixtures/vault.yml test/fixtures/modified_vault.yml')

  execute 'edit test/fixtures/modified_vault.yml'
  doautocmd SafeState

  call setline(1, 'secret_key: changed_value')
  write

  bwipeout!

  let decrypted = system('ansible-vault view test/fixtures/modified_vault.yml')
  Assert decrypted =~ 'changed_value'

  call delete('test/fixtures/modified_vault.yml')


Execute (Plain YAML matching vault pattern is not decrypted):

  execute 'edit test/fixtures/plain_vault.yml'
  doautocmd SafeState

  let content = readfile('test/fixtures/plain_vault.yml')
  Assert getline(1) ==# content[0]

  bwipeout!


Execute (Security options are set for vault files):

  execute 'edit test/fixtures/vault.yml'

  Assert !&swapfile
  Assert !&backup
  Assert !&writebackup
  Assert &viminfo ==# ''

  bwipeout!


Execute (Unchanged vault content is not re-encrypted on write):

  execute 'edit test/fixtures/vault.yml'
  doautocmd SafeState

  let before = readfile('test/fixtures/vault.yml')
  setlocal modified
  write
  let after = readfile('test/fixtures/vault.yml')

  " ansible-vault always uses a fresh IV, so ciphertext differs on each
  " encrypt; identical file means the write was skipped
  Assert before ==# after

  bwipeout!


Execute (Buffer is not modified after decryption):

  execute 'edit test/fixtures/vault.yml'
  doautocmd SafeState

  Assert !&modified

  bwipeout!


Execute (Custom vault pattern is used when g:ansible_vault_pattern is set):

  unlet g:loaded_ansible_vault_auto
  let g:ansible_vault_pattern = '*secrets.yml'
  source plugin/ansible_vault.vim

  Assert  exists('#AnsibleVault#BufReadPost#*secrets.yml')
  Assert !exists('#AnsibleVault#BufReadPost#*vault.yml')

  " Restore default pattern for subsequent tests
  unlet g:loaded_ansible_vault_auto
  unlet g:ansible_vault_pattern
  source plugin/ansible_vault.vim


Execute (ansible-vault error message is shown when decrypt fails):

  execute 'edit test/fixtures/corrupted_vault.yml'
  try
    doautocmd SafeState
    Assert 0, "Expected an error to be thrown"
  catch /Vault Decryption Failed/
    " ansible-vault diagnostic (e.g. "[ERROR]: Vault format...") should be included
    Assert v:exception =~ '\[ERROR\]'
  endtry

  " Buffer should still contain the original (undecrypted) content
  let content = readfile('test/fixtures/corrupted_vault.yml')
  Assert getline(1) ==# content[0]

  bwipeout!


Execute (Empty vault content opens without error):

  execute 'edit test/fixtures/empty_vault.yml'
  doautocmd SafeState

  Assert getline(1) ==# ''
  Assert !&modified

  bwipeout!


Execute (Write is aborted when existing vault cannot be read):

  call system('cp test/fixtures/corrupted_vault.yml test/fixtures/corrupted_write_vault.yml')

  execute 'edit test/fixtures/corrupted_write_vault.yml'
  try
    doautocmd SafeState
  catch
  endtry

  " Force a write attempt on the undecrypted buffer
  setlocal modified
  try
    write
  catch
  endtry

  " File on disk should be unchanged (write was aborted)
  let after = readfile('test/fixtures/corrupted_write_vault.yml')
  Assert after[0] ==# '$ANSIBLE_VAULT;1.1;AES256'

  bwipeout!
  call delete('test/fixtures/corrupted_write_vault.yml')


Execute (Encryption error message includes ansible-vault stderr output):

  " A path in a non-existent directory causes ansible-vault encrypt to fail,
  " producing output on stderr. Verify that output is included in the error.
  execute 'edit /nonexistent/dir/error_vault.yml'
  call setline(1, 'secret_key: value')

  let caught = ''
  try
    write
  catch
    let caught = v:exception
  endtry

  Assert caught =~# 'Encryption failed', "Write should throw an encryption error"
  Assert caught =~# 'Encryption failed: \S', "Encryption error should include stderr output"

  bwipeout!
